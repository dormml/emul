
:   ints-check
    ints-on ints-off
;

\   Last VDP status
1       RamVar vdp-status

\   Timer
2 cells RamVar    vdp-ticks 
cell    RamVar    vdp-timeout

:   v-intflag-off?  ( mask -- t|f )
    v-intflags c@  AND  0=
;

:   vdp-int-handler 
    'INTS c@  
    dup M_INT_VDP and if 
        \ acknowledge interrupt
        [ M_INT_VDP invert literal ] and  'INTSP c!
        
        \ acknowledge VDP
        VDPST c@  vdp-status c!
        
        \ timer
        1. vdp-ticks d+!

        \ timeout
        1 vdp-timeout +!
        vdp-timeout @ 0= if
            false vid-show
        then  
        
        \ keyboard timer
        kbdtimer c@ 1+ kbdtimer c!    
        
        \ scan keyboard
        
        kbd-scan
        
        \ do other (expensive) things if interrupts allow
        v-int-nocursor v-intflag-off? if 
            update-cursor
        then
        
        \ acknowledge interrupt
        \ 'INTS c@  [ M_INT_KBD invert literal ]  and  'INTSP c!
    else
        drop
    then
    
    exiti
;

:   kbd-int-handler
    'INTS c@  
    dup M_INT_KBD and if
        \ [char] K demit
     
        \ acknowledge interrupt
        [ M_INT_KBD invert literal ]  and  'INTSP c!
        
        kbd-scan
    else
        drop
    then
    exiti
;

:   nmi-int-handler
    \ acknowledge all interrupts
    $00 'INTSP c!
    \ reset mask
    0  (>context) [ CTX_SR field, ] 
    
    $00 msx-cmdsetup
    ."  aborted" cr
    abort
    exiti       \ NOTREACHED
;

:   fault-int-handler   ( rp sp st pc -- )
    s" FAULT" v-screen @  swap  cvmove
    begin (idle) again
    exiti
;

:   >int-vec ( num -- )
    2*  IntVecs +
;
:   set-int-vec ( addr num -- )
    >int-vec  !
;

:   enable-int ( addr num -- )
    dup >r   set-int-vec

    \ enable bit
    'INTS c@  1 r> lshift  or  'INTS c!

;

:   call-int    ( num -- )
    >int-vec @  
    7 (context>) [ CTX_INT field, ] >r 
    execute
;

:   ints-init
    ints-off
 \   ['] kbd-int-handler     INT_KBD     enable-int
    ['] fault-int-handler   INT_FAULT  set-int-vec
    ['] bootup              INT_RESET   set-int-vec
    ['] nmi-int-handler     INT_NMI     set-int-vec
    ['] vdp-int-handler     INT_VDP     enable-int
;

