
(
To deal uniformly with text and graphics modes, the video
mode data uses this data for the attributes and methods 
associated with each mode.
 
For use by the terminal, characters are located by a video-mode-specific
position in a video-mode-specific window.  I.e. for text modes
they represent characters, and for graphics modes, they 
represent pixels.  These are called "units" here.

The video mode determines how to advance the cursor based on
the current font.
)

0      RamVar  v-data

v-data
    +field  v-screen       \ VDP addr of screen
    +field  v-screensz     \ VDP size of screen
    +field  v-patts        \ VDP addr of patterns
    +field  v-pattsz       \ VDP size of pattern table
    +field  v-colors       \ VDP addr of colors
    +field  v-colorsz      \ VDP size of color table
    +field  v-sprites      \ VDP addr of sprites
    +field  v-sprcol       \ VDP addr of sprite color table (0 if not sprite 2 mode)
    +field  v-sprpat       \ VDP addr of sprite patterns
    +field  v-sprmot       \ VDP addr of sprite motion
    +field  v-free         \ usable space
    
    +1field v-text         \ set if mode is text-based
    +1field v-mono         \ set if monochrome (colors are global)
    
    +field  v-width        \ units across
    +field  v-height       \ units down
    
    +field  v-font         \ GROM addr of current font
    +1field v-fntwidth     \ font width in pixels
    +1field v-fntheight    \ font height in pixels

    +1field  v-intflags    \ video interrupt flags
    +1field  v-blink       \ if set, set blink flag for text2 chars 
    
    +field  'v-drawchar     ( ch x y -- )
    +field  'v-readchar     ( x y -- ch )
    +field  'v-blinkcursor   ( x y -- )
    +field  'v-setupmode    ( -- )
    +field  'v-updatecolors ( -- )
    +field  'v-setfont      ( addr -- )
    +field  'v-clear        ( ch -- )
    +field  'v-hchar        ( ch len x y -- )
    +field  'v-vchar        ( ch len x y -- )

    +field  msx-stride      \ row stride in bytes
    +field  msx-shift       \ shift for pixel >> byte
    +field  msx-mask        \ mask for pixel of byte

    16 +Nfield v-regsave    \ saved registers

dup constant v-data0

dup v-data - negate  Constant v-data-size

ramptr ! 

$80 constant    v-int-nosprmot
$40 constant    v-int-nocursor

\ -------------------------------------------------------------------------

:   >vr         ( val reg -- )
    dup 16 <  if  v-regsave + c!  else 2drop then    
;

:   (vr!)       ( val reg -- )
    2dup >vr
    
    \ write VDP addr
    2dup  8 lshift $8000 OR  OR  vwaddr
;


:   v-regaddr ( reg -- )
    dup  
    vwaddr
    $7fff and w>b   >vr
;

:   (vr@) ( reg# -- val )
    v-regsave + c@
;

\   (Not a good reason for locals, since they're much slower, but just have something for now)

::   write-vregs ( reglist -- )
    begin 
        reglist @ dup
    while
        v-regaddr
        2 'reglist +!
    repeat   
    drop
;

: vid-show ( f -- )
    $40 and 
    1 (vr@)  or
    $8100 or  v-regaddr
;

\ ---------------------

: >v-fontaddr   ( ch -- )
    v-fntheight c@ u*  v-font @ +
;

\ Save space for content under the blinking cursor 
8   RamVar v-curs-under  

1   RamVar v-curs                         \ state of cursor (0=off, $80=on)
1   RamVar v-cursor-timer                 \ current iter

20 Constant v-cursor-blink              \ 30/60 sec
 
:   write-var-list   ( table -- )
    begin
        dup @ 
    while
        dup  d@  swap !
        4 +
    repeat
    drop
;

: (mode)
    dup  0 last-video-mode within  0= err-out-of-range ?error
    cells  video-modes +  @ 
    
    dup 0= err-undefined ?error
    
    v-font @  v-fntwidth c@  v-fntheight c@
    0  v-data v-data-size -1  (cfill)     \ v-data is end 
    v-fntheight c!  v-fntwidth c!  v-font !
    
    execute
;

1   RamVar fg
1   RamVar bg

: color-byte
    fg c@ 4 lshift  bg c@ $f and  or
;
: color-word
    fg c@ 8 lshift  bg c@ $ff and  or
;

: v-refresh-colors
    'v-updatecolors @  execute
;

1 <export
: fg!
    $ff and  fg c!
    v-refresh-colors
;

: bg!
    $ff and  bg c!
    v-refresh-colors
;

: fg@
    fg c@
;   

: bg@
    bg c@
;

export>

\   Reset video state
\
\   -- reset terminal bounds
\   -- clear memory
\   -- load font (if needed)

: vreset
    'v-setupmode @  execute
    
    v-refresh-colors
    
    (setfont)

    term-reset
    'v-clear @ execute    
    
    true vid-show
;

: vstdpal

;


:   v-cursor-off  ( x y -- )
    v-curs c@ if
        'v-blinkcursor @ execute
    else
        2drop
    then
;

:   update-cursor
    1 v-cursor-timer c+!
    v-cursor-timer c@  v-cursor-blink >= if
        0 v-cursor-timer c!
        $xy
        'v-blinkcursor @ execute
    then
;


\ ---------------------

Create video-modes
    ' text-mode ,
    ' gfx-mode ,
    ' bit-mode ,
    ' gfx3-mode ,
    ' gfx4-mode ,
    ' gfx5-mode ,
    ' gfx6-mode ,
    ' gfx7-mode ,
    ' text2-mode ,
    ' mono-mode ,
    0 ,
    
1 <export
$000    constant    text
$001    constant    gfx
$002    constant    bitmap      $002    constant    gfx2
$003    constant    gfx3           
$004    constant    gfx4
$005    constant    gfx5
$006    constant    gfx6
$007    constant    gfx7
$008    constant    text2
$009    constant    mono
$00A    constant    multi
export>

$00B    constant    last-video-mode

1 <export
\ tables for (vtab)
&0 constant #scr
&1 constant #ssz
&2 constant #col
&3 constant #csz
&4 constant #pat
&5 constant #psz
&6 constant #spt
&7 constant #spr
&8 constant #smt
&9 constant #spc
&10 constant #fre
export>

include video_text.fi
include video_bit.fi
include video_msx.fi


\ ---------------------

1 <EXPORT

:   mode ( num -- )
    (mode) vreset vstdpal
;


:   vfill ( ch addr len -- )
    swap +!vwaddr
    VDPWD swap 0 (cfill) 
;

:   (setfont)
    v-font @  'v-setfont @  execute
    term-refresh
;

:   font8x8
    grom_font8x8 v-font !
    8 v-fntwidth c!   8 v-fntheight c!
    (setfont)
;
:   font6x8
    grom_font8x8 v-font !
    6 v-fntwidth c!   8 v-fntheight c!
    (setfont)
;
:   font5x6
    grom_font5x6 v-font !
    5 v-fntwidth c!   6 v-fntheight c!
    (setfont)
;

: <video    ( -- flags )
    cursor-off
    v-intflags c@
    [ v-int-nosprmot v-int-nocursor or literal ] v-intflags c! 
;

: video>  ( oldflags -- )
    v-intflags c!
;

( get size of save area )
:   #vrs ( -- n )
    v-data-size t-data-size +
;

( save video state: needs buffer of v-data-size size )
:: vsave  ( buff -- )
    v-data0  buff                 v-data-size  cmove 
    t-data0  buff  v-data-size +  t-data-size  cmove 
;

( restore video state: needs buffer of v-data-size size )
:: vrestore  ( buff -- )
    buff  v-data0                 v-data-size  cmove 
    buff  v-data-size +  t-data0  t-data-size  cmove
    
    16 0 do
        v-regsave i + c@  $8000  i 12 lshift  OR  OR  vwaddr
    loop 
;

: vblink!  ( flag -- )
    v-blink c!
;

EXPORT>

\ TODO
: vpage drop ;


:   video-init
    \ reset latches
    VDPRD c@ drop
    0 GPLWD c! 

    0 v-intflags c!
        
    text mode

    $1 fg!
    $7 bg!
    
    font8x8    
    
;


