*     UTILS
*
*     Utilities for common VDP/GPL
*     routines.
*

*
*     IMPORTANT:  BLWP @GPLSET to
*	set up GPLLNK for later use.
*

WAD LI R1,>4000
 JMP $+4
WAD2 CLR R1
 MOV *R13,R2
 MOVB @VWP+5,@>8C02
 SOC R1,R2
 MOVB R2,@>8C02
 MOV @2(R13),R1
 MOV @4(R13),R2
 RT
VWP BSS 32
VMBW DATA VWP,VMBW+4
 BL @WAD
VMBW1 MOVB *R1+,@>8C00
 DEC R2
 JGT VMBW1
 RTWP
VMBR DATA VWP,VMBR+4
 BL @WAD2
VMBR1 MOVB @>8800,*R1+
 DEC R2
 JGT VMBR1
 RTWP
VSBR DATA VWP,VSBR+4
 BL @WAD2
 MOVB @>8800,@2(R13)
 RTWP
VSBW DATA VWP,VSBW+4
 BL @WAD
 MOVB @2(R13),@>8C00
 RTWP
VWTR DATA VWP,VWTR+4
 MOV *R13,R0
 ORI R0,>8000
 SWPB R0
 MOVB R0,@>8C02
 SWPB R0
 MOVB R0,@>8C02
 RTWP
 
EASVLN DATA 0
EASVFN DATA 0
EASV1 DATA 0
EASV2 DATA 0
EASV3 DATA 0
DTEMP BSS 10
DSRWP BSS   32
EQUALS DATA >2000
PERIOD BYTE '.'
VERIF BYTE >AA
RTSAVE DATA 0
DSRLNK DATA DSRWP,DSRLNK+4
 MOV   *14+,5
 SZCB  @EQUALS,R15
 MOV   @>8356,R0
 MOV   R0,R9
 AI    R9,>FFF8
 BLWP  @VSBR
 MOVB  R1,R3
 SRL   R3,>8
 SETO  R4
 LI    R2,DTEMP
GDLOOP
 INC   R0
 INC   R4
 C     R4,R3
 JEQ   GOTDEV
 BLWP  @VSBR
 MOVB  R1,*R2+
 CB    R1,@PERIOD
 JNE   GDLOOP
GOTDEV
 MOV   R4,R4
 JEQ   DERR
 CI    R4,>0007
 JGT   DERR
 CLR   @>83D0
 MOV   R4,@>8354
 MOV   R4,@EASVLN
 INC   R4
 A     R4,@>8356
 MOV   @>8356,@EASVFN
 LWPI  >83E0
 CLR   R1
 LI    R12,>0F00
CRUNRD
 MOV   R12,R12
 JEQ   CRUNS
 SBZ   >0000
CRUNS
 AI    R12,>0100
 CLR   @>83D0
 CI    R12,>2000
 JEQ   DNDEV
 MOV   R12,@>83D0
 SBO   >0000
 LI    R2,>4000
 CB    *R2,@VERIF
 JNE   CRUNRD
 A     @DSRWP+10,R2
 JMP   CLOOK
CNXNAM
 MOV   @>83D2,R2
 SBO   >0000
CLOOK
 MOV   *R2,R2
 JEQ   CRUNRD
 MOV   R2,@>83D2
 INCT  R2
 MOV   *R2+,R9
 MOVB  @>8355,R5
 JEQ   CSKIP
 CB    R5,*R2+
 JNE   CNXNAM
 SRL   R5,>8
 LI    R6,DTEMP
CNXCHR
 CB    *R6+,*R2+
 JNE   CNXNAM
 DEC   R5
 JNE   CNXCHR
CSKIP
 INC   R1
 MOV   R1,@EASV1
 MOV   R9,@EASV2
 MOV   R12,@EASV3
 BL    *R9
 JMP   CNXNAM          JMP TO >3BFA
 SBZ   >0000
 LWPI  DSRWP
 MOV   R9,R0
 BLWP  @VSBR
 SRL   R1,>D
 JNE   DWERR
 RTWP
DNDEV
 LWPI  DSRWP
DERR
 CLR 1
DWERR
 SWPB 1
 MOVB 1,*13
 SOCB @EQUALS,15
 RTWP
 
* GPLLNK ROUTINES ADAPTED FROM
* PUBLIC DOMAIN ROUTINES BY
* PAUL CHARLTON
 
GRMSAV DATA 0
GPLLNK DATA VWP,GPLLNK+4
 MOVB @>8373,1
 SRL 1,8
 AI 1,>8302
 MOV @GRMSAV,*1
 SWPB 1
 MOVB 1,@>8373
 MOVB *14+,@>9C02
 NOP
 MOVB *14+,@>9C02
 LI 0,RTFGPL
 MOV 0,@>8300
 LWPI >83E0
 B @>6A
RTFGPL
 LWPI VWP
 RTWP
 
GPLSET DATA VWP,GPLSET+4
 CLR 0
 LI 8,>F00
 LI 9,>F000
GAG MOVB 0,@>9C02
 SWPB 0
 MOVB 0,@>9C02
 SWPB 0
 MOVB @>9800,1
GAG1
 CB 1,8
 JEQ GFND
 INC 0
 JNO GAG
 BLWP @0
 
GFND
 INC 0
 MOVB @>9800,1
 CB 1,9
 JNE GAG1
 DEC 0
 MOV 0,@GRMSAV
 RTWP
 
KSCAN DATA VWP,KSCAN+4
 LIMI 1
 LIMI 0
 LWPI >83E0
 MOV R11,@VWP+22
 BL @>E
 LWPI VWP
 MOV R11,@>83F6
 RTWP

