\   core_dict.fi
\ 
\   (c) 2014 Ed Swartz
\ 
\   All rights reserved. This program and the accompanying materials
\   are made available under the terms of the Eclipse Public License v1.0
\   which accompanies this distribution, and is available at
\   http://www.eclipse.org/legal/epl-v10.html
\ 

\   Dictionary entry:
\
\   [ word: previus entry ]
\   [ NFA: byte flags + length; name; space terminator to align ]
\   XT (under direct threading).  For code words, the code directly
\   lives here.  Colon words contain "BL *DOCOL" (R8).
\   Does words contain "BL *DODOES" (R7).


: nfa>imm?  ( nfa -- t/f )
    c@ |immed and 0<>
;

: nfa>xt
	dup c@ $1f AND  \ get the length byte
        + 1+ aligned
;

: lfa>nfa
    cell+
;

: >id       ( nfa -- caddr u )
    count $1f and 
;

: id.       ( nfa -- )
    >id type space
;

\   \\\\\\\\\\\\\\\\\

[IFUNDEF] IMMEDIATE
: IMMEDIATE
    latest lfa>nfa dup c@ |immed or swap c!
;
[THEN]


[IFUNDEF] (lfind)

| : (lfind)      ( c-addr lfa -- c-addr 0 | xt -1==immed | xt 1 ) 
    \ find word in dictionary  
    \ lfa is nfa-2
    
    swap >r
    
    begin
        dup
    while
        r@ over  cell+             \ nfa
        nfa=
        if
            rdrop
            cell+
            
	        dup nfa>xt 
	        swap c@ 
	        |immed and if 1 else -1 then
	        
            exit
        else
            @            
        then
    repeat
    drop r> 0
;

| : nfa=  ( caddr nfa -- 1 | 0 )
    dup c@  |srch and if   \ not hidden?
        2>r
        r@  1+  r>  c@ $3f and  \ nfa --> caddr n
        r@  1+  r>  c@          \ caddr -> caddr n
        s<>  0=
    else
        2drop 0
    then
    
;

[THEN]

' (lfind)	TO	(find)


\ Hide a word while compiling, to allow redefining a word in terms of the same name
| : rom-SMUDGE
    lastnfa 
    dup c@ |srch xor swap c!
;

' rom-SMUDGE 	to SMUDGE

| : rom-NAME, ( "name" -- )

    \ Put LFA --> ptr to previous LFA
	here
    latest ,

    name"
    
    \ Get space for name
    c@ &nmask min
    1+ aligned allot

[ [IFDEF] (register-symbol) ]
    dup (register-symbol)
[ [THEN] ]
    
    \ current @ !       \ !!!
    >latest !
    
;

: XT, ( xt -- )
	drop
;


' rom-NAME, 	to NAME,
