\   utils.fi
\ 
\   (c) 2011 Edward Swartz
\ 
\   All rights reserved. This program and the accompanying materials
\   are made available under the terms of the Eclipse Public License v1.0
\   which accompanies this distribution, and is available at
\   http://www.eclipse.org/legal/epl-v10.html
\ 

: per-line
    \ 7 = 4 addr + = + space + NL
    win-cx c@  7 -  2 urshift   1 nand  1 max
;

: 2u.
    0 <# # # #> type
;

: 4u.
    0 <# # # # # #> type
;

\ dump one line
| : (dmpln) ( addr cnt xt -- )
    >r  \ save xt
    ( addr cnt )
    over 4u.   [char] = emit  
    2dup over + swap
    do 
        i j execute 2u. space
    loop    
    ( addr cnt )
    per-line over ?do 3 spaces loop
    ( addr cnt )
    0 ?do dup i + j execute dup $20 $7f within 0= 
            if drop [char] . then emit 
    loop drop
    rdrop
    cr
;


| : (quit?)
    dup &81 = swap &113 = or
;

| : (pause?)  ( -- <t|f to quit> )
    key? dup if            
        ( t ) 
        key (quit?) 0= if       \ not quit
            \ pause and wait for something else 
            drop key (quit?)
        else
            ( t )
        then
    \ else false, no key, so no quit        
    then
;



:   digit ( n -- ch )
    dup 9 > if 55 else 48 then + 
;

:   d.d  ( n -- )
    <# sign # #s #> dtype
;

:   .d  ( n -- )
    s>d d.d
;

| : (dmp) ( addr cnt xt -- )
    base @ >r hex
    >r cr
    over +   ( addr addr+cnt )
    swap
    do
        i per-line 
        j (dmpln)
        (pause?) if leave else per-line then
    +loop
    rdrop
    r> base !
;

: dump
    ['] c@ (dmp)
;

: vdump
    ['] +vc@ (dmp)
;

: gdump
    ['] gc@ (dmp)
;

: mfree
    ram @  here  -  
;
    